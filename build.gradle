plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group = 'org.degree'
version = '0.2.0-NOTRELEASED'
description = 'Plugin for factions'

repositories {
    mavenCentral()
    maven {
        name = "spigotmc-repo"
        url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        url = 'https://repo.extendedclip.com/releases/'
    }
}

dependencies {
    compileOnly "org.spigotmc:spigot-api:1.21-R0.1-SNAPSHOT"

    compileOnly 'me.clip:placeholderapi:2.11.6'

    implementation 'org.ow2.asm:asm:9.5'
    implementation 'org.json:json:20210307'

    implementation 'org.bstats:bstats-bukkit:3.0.0'

    implementation 'javax.servlet:javax.servlet-api:4.0.1'
    implementation 'org.eclipse.jetty:jetty-server:9.4.43.v20210629'
    implementation 'org.eclipse.jetty:jetty-servlet:9.4.43.v20210629'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.release.set(17)
}

tasks.register('copyWebResources', Copy) {
    from 'src/main/resources/web'
    into "$buildDir/resources/main/web"
}

processResources {
    inputs.properties version: project.version
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand version: project.version
    }
    from('src/main/resources/web') {
        into 'web'
    }
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

shadowJar {
    configurations = [project.configurations.runtimeClasspath]

    relocate 'org.eclipse.jetty', 'org.degree.faction.lib.jetty'
    relocate 'org.bstats', 'org.degree.libs.bstats'

    archiveFileName.set('Factions-0.2.0-NOTRELEASED.jar')

    manifest {
        attributes(
                'Implementation-Title': project.name,
                'Implementation-Version': project.version,
                'Author': 'Degree',
                'Description': project.description
        )
    }
}
